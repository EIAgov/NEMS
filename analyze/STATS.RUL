*                ::: STATS.RUL  7-26-90 :::

     Statistical Summary of %PROBLEM Scenario
$TEXT CENTER
$ANALYZE SUB ROW * N=0
$SET NR0 = %NROWS
$ANALYZE SUB COL * N=0
$SET NC0 = %NCOLS
$ANALYZE SUBMAT *
$SET NR = %NROWS
$SET NC = %NCOLS
$SET NZ = %NONZERO
$IF %%NR0 = 0 THEN GOTO R0END
$CALC NR = NR + %%NR0
$TEXT 1 MARGIN=5
 The following rows are null.
$ANALYZE DISP R * N=0 //
:R0END

$IF %%NC0 = 0 THEN GOTO C0END
$CALC NC = NC + %%NC0
$TEXT 1 MARGIN=5
 The following columns are null.
$ANALYZE DISP C * N=0 //
:C0END

$ANALYZE SUBMAT R * L EQ U
*                   :...........Equations
$SET NEQ = %NROWS
$ANALYZE SUBMAT CO *
$ANALYZE SUBMAT R * L=-*  U=*
*                   :...........Free rows
$SET NFR = %NROWS
$SET NLU = %%NR
$CALC NLU = NLU - %%NFR - %%NEQ
*     :.........L-rows, G-rows and ranges

$ANALYZE SUBMAT R *
$ANALYZE SUBMAT CO * L EQ U
*                    :........Fixed columns
$SET NFX = %NCOLS
$ANALYZE SUBMAT R *
$ANALYZE SUBMAT CO * L=0 U=*
*                    :........Vanilla
$SET NV = %NCOLS
$SET NBD = %%NC
$CALC NBD = NBD - %%NV - %%NFX
*     :........Bounded, but not fixed

$TEXT 1 MARGIN=5
                  Dimensions
$TEXT CENTER
                  ~~~~~~~~~~
$TEXT CENTER
   %%NR Rows.......... %%NEQ Equations, %%NLU Inequalities, %%NFR Free
$TEXT
   %%NC Columns....... %%NV Vanilla, %%NBD Bounded, %%NFX Fixed
$TEXT
   %%NZ Nonzeroes..... %NVALUES Distinct, %NONES ones, Density = %VDENSTY
        percent
$TEXT

*  Get Embedded Netform
$ANALYZE BLOCK R E *
$ANALYZE BLOCK C E *
$ANALYZE SUBMAT *
$ANALYZE BLOCK NET
$IF %NCBLKS > 0 THEN SKIP 3
(There is no embedded netform.)
$TEXT 2
$GOTO SOLN

$LOOKUP BLOCK COL NET.PROD
$SET #PROD = %VLOOK
$LOOKUP BLOCK COL NET.TRAN
$SET #TRAN = %VLOOK
$LOOKUP BLOCK COL NET.CONS
$SET #CONS = %VLOOK

$ANALYZE SUBMAT CLEAR
* Copy Netform to ANALYZE SUBMATrix
$IF %%#PROD > 0 THEN ANALYZE BLOCK COL COPY FROM NET.PROD
$IF %%#CONS > 0 THEN ANALYZE BLOCK COL COPY FROM NET.CONS
$IF %%#TRAN > 0 THEN ANALYZE BLOCK COL COPY FROM NET.TRAN
$ANALYZE SUBMAT R *
$ANALYZE SUBMAT SET
$TEXT
  Embedded Netform is %NROWS x %NCOLS :
$TEXT 1 MARGIN=2
 ...%%#PROD Production, %%#CONS Consumption, %%#TRAN Transfer activities.
$TEXT
$IF %STATUS = OPTIMAL THEN SKIP 2
    Objective: %OPT %OBJ ...Resident Status = %STATUS .
$EXIT
$TEXT 1 MARGIN=1

:SOLN
                Solution Statistics
$TEXT CENTER
                ~~~~~~~~~~~~~~~~~~~
$TEXT CENTER

$ANALYZE SUBMAT R %OBJ
    Objective:     %OPT(1:3) %OBJ = %VROWY

$ANALYZE SUBMAT R * S B Y EQ U
$SET NR = %NROWS
$ANALYZE SUBMAT R * S B Y EQ L
$CALC NR = NR + %NROWS
$ANALYZE SUBMAT CO * S B X EQ L
$SET NC = %NCOLS
$ANALYZE SUBMAT CO * S B X EQ U
$CALC NC = NC + %NCOLS
    Primal Degeneracies:  Rows = %%NR   Columns = %%NC
$ANALYZE SUBMAT R  * S LU P=0
$ANALYZE SUBMAT CO * S LU D=0
    Dual Degeneracies:    Rows = %NROWS   Columns = %NCOLS
$ANALYZE SUBMAT R  * S=LU
$ANALYZE SUBMAT CO * S=BI
    The basis has %NROWS binding rows (and %NCOLS basic structurals)
    with %NONZERO nonzeroes.
$EXIT
                             ::: END STATS.RUL :::
