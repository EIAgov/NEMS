*            ::: PRICES.RUL  7-26-90 :::

* THIS ATTEMPTS TO INTERPRET PRICE OF A SUPPLY ROW

* ASSUMES VARIABLES:  ROW, PRICE

$SET MT = %%ROW(2:3)
$SET LO = %%ROW(4:5)
$SET ND = 0
* SET SUPPLY COLUMN (SUPCOL)
*  (LOOP BECAUSE WE DO NOT KNOW WHAT STEP)
$LOOP
$IF %COLUMN(1:5) = S%%MT%%LO THEN SET SUPCOL = %COLUMN
$IF %COLUMN(1:5) <> T%%MT%%LO THEN SKIP LOOP
$IF %VCOLX > 0 THEN CALC ND = ND + 1
$NEXT C

$LOOKUP C %%SUPCOL
$SET SCOST = %VLOOK
$LOOKUP D %%SUPCOL
$SET RENT = 0
$CALC RENT = 0 - %VLOOK
  The (input) supply cost = %%SCOST
$IF %%RENT > 0 THEN SKIP 2
= the marginal supply price.
$EXIT
(excluding economic rent = %%RENT ).  This supply is delivered to
$SET PLURAL
$IF %%ND = 1 THEN SKIP 4
$SET PLURAL = s
%%ND consumers -- namely, to
$SET SPRICE = %%SCOST
$CALC SPRICE = SPRICE + %%RENT

* LOOP OVER SUBMATRIX TO OBTAIN CONSUMERS ACTIVELY LINKED TO THIS SUPPLIER

$LOOP
$IF %COLUMN(1:5) <> T%%MT%%LO THEN SKIP LOOP
$IF %VCOLX = 0 THEN SKIP LOOP
$SET ADEM = %COLUMN(6:7)
$ENTITY %COLUMN(6:7) DR
 with transportation cost = %VCOLC
$IF %%ND = 1 THEN ENDLOOP
; and, to
$CALC ND = ND - 1
$NEXT C
.

* Look at degeneracies to get consumer causing rent.
$ANALYZE SUBMAT R %%ROW
$ANALYZE SUBMAT CO T%%MT%%LO S B X=0
$ANALYZE SUBMAT SET
$IF %NONZERO > 0 THEN GOTO CONT
 You might try interpreting the consumer price%%PLURAL .
 ...see row D%%MT%%ADEM .
$EXIT

:CONT
* WE HAVE A DEGENERATE TRANSPORTATION ACTIVITY IN THIS SUPPLY ROW
$IF %NCOLS > 1 THEN GOTO MORE

* ONLY 1 DEGENERATE COLUMN
$SET SCONS = %COLUMN(6:7)
 This supplier does not send any
$ENTITY %%MT MT
to
$ENTITY %%SCONS DR
, but it is this consumer that is setting the supplier economic rent.
  That is, any increase in the supply of
$ENTITY %%MT MT
in
$ENTITY %%LO SR
would go to
$ENTITY %%SCONS DR
and displace its most expensive current delivered cost.
$EXIT

:MORE
* MORE THAN 1 DEGENERATE TRANSPORTATION ACTIVITY IS ACTIVE FROM SUPPLIER
$SET CONJUN = ,
$SET NUMBER = %NCOLS
 There are %NCOLS consumers that do not receive any
$ENTITY %%MT MT
from this supplier, but one of them is setting the supplier rent.
 These consumers are:
$LOOP
$ENTITY %COLUMN(6:7) DR
%%CONJUN
$CALC NUMBER = NUMBER - 1
$IF NUMBER = 2 THEN SET CONJUN = and
$IF NUMBER = 1 THEN SET CONJUN = .
$NEXT C
 That is, any increase in the supply of
$ENTITY %%MT MT
in
$ENTITY %%LO SR
would go to one of these consumers and displace
its most expensive current delivered cost.  I suggest you interpret one
of these consumer prices.
$EXIT

                ::: END PRICES.RUL :::
