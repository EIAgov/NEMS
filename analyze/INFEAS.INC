* INCLUDE FILE FOR INFEAS.RUL
* LAST CHANGE: 11-29-92
* MAIN RULE FILE MUST HAVE :EXIT LABEL

$IF %NRBLKS > 0 THEN GOTO BEGIN
The IIS file has not been read in.
$GOTO ENDIIS
:BEGIN
$ANALYZE SUBMAT CLEAR

 The following constraints are inconsistent.
$LOOKUP BLOCK ROW IIS.LO
$IF %VLOOK > 0 THEN ANALYZE BLOCK R C F IIS.LO
$LOOKUP BLOCK ROW IIS.UP
$IF %VLOOK > 0 THEN ANALYZE BLOCK R C F IIS.UP
$LOOKUP BLOCK ROW IIS.LU
$IF %VLOOK > 0 THEN ANALYZE BLOCK R C F IIS.LU
$ANALYZE SUB COL *
$ANALYZE LIST  //E
* PUT IIS ROWS INTO ONE BLOCK (FOR EXPLAIN)
$ANALYZE BLOCK R E *
$ANALYZE BLOCK R C TO IIS

$SET NUMBER = 3
*             :...number of column bounds displayed per line
$SET LC = %%NUMBER
*    :...Line count (room remaining)...new line when LC becomes 0
$ANALYZE SUBMAT D C *
$STACK
$SET BOUND = Column Bounds:

$LOOKUP BLOCK COL IIS.LO
$IF %VLOOK = 0 THEN GOTO UP
$ANALYZE BLOCK C C F IIS.LO
* DELETE COLUMNS IN IIS WHOSE LOWER BOUND IS 0
$ANALYZE SUB D C * L=0
$IF %NCOLS = 0 THEN GOTO UP

$LOOP
  %%BOUND  %COLUMN >= %VCOLLO
$SET BOUND =;
$PUSH 1 %COLUMN
$IF %%LC = 0 THEN SKIP 2
$CALC LC = LC - 1
$SKIP LOOP
$TEXT
$SET LC=%%NUMBER
$NEXT COL
$ANALYZE SUBMAT D C *

:UP
$LOOKUP BLOCK COL IIS.UP
$IF %VLOOK = 0 THEN GOTO LU
$ANALYZE BLOCK C C F IIS.UP
$LOOP
  %%BOUND  %COLUMN <= %VCOLUP
$SET BOUND =;
$PUSH 1 %COLUMN
$IF %%LC = 0 THEN SKIP 2
$CALC LC = LC - 1
$SKIP LOOP
$TEXT
$SET LC = %%NUMBER
$NEXT COL
$ANALYZE SUB D C *

:LU
$LOOKUP BLOCK COL IIS.LU
$IF %VLOOK = 0 THEN GOTO DONE
$ANALYZE BLOCK C C F IIS.LU

$LOOP
$IF %VCOLLO < %VCOLUP THEN SKIP LOOP
  %%BOUND  %COLUMN = %VCOLLO
$SET BOUND =;
$PUSH 1 %COLUMN
$IF %%LC = 0 THEN SKIP 2
$CALC LC = LC - 1
$SKIP LOOP
$TEXT
$SET LC = %%NUMBER
$NEXT COL

$LOOP
$IF %VCOLLO = %VCOLUP THEN SKIP LOOP
$TEXT
  %%BOUND  %VCOLLO <= %COLUMN <= %VCOLUP ...only one of these is in IIS
$SET BOUND =;
$PUSH 1 %COLUMN
$NEXT COL

:DONE
$ANALYZE SUBMAT CLEAR
$ANALYZE BL R C F IIS
$LOOP
:NEXTCOL
$POP 1 COL
$ANALYZE SUBMAT A C %%COL
$IF %NSTACK1 > 0 THEN GOTO NEXTCOL
$ENDLOOP

The IIS rows and columns have been put into the submatrix.
Use the EXPLAIN command to see their meanings.
$GOTO EXIT
* We get here in case of error (fall thru in rule file).
:ENDIIS
