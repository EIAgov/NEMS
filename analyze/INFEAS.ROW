* INFEAS.ROW IS INCLUDE FILE FOR INF$ABP.RUL
* ...MASTER RULE FILE MUST HAVE EXIT LABEL
* TRY EXPLORING INFEASIBLE ROWS

* RESERVE A ROW BLOCK CALLED TEMP
$ANALYZE SUB R %OBJ
$ANALYZE BL R C TO TEMP

$ANALYZE SUB R * S=I
$IF %NROWS = 0 THEN GOTO RETURN
* BLOCK THE INFEASIBLE ROWS
$ANALYZE SUB COL *
$ANALYZE SUB SET
$ANALYZE BL
$SET COMP = %NCBLKS
$IF %%COMP > 9 THEN SET COMP = 9
$SET MINROW = 20
$SET MINCOL = 40
$SET MINBLK = 0

$LOOP
:NEXTBLK
$ANALYZE SUB CLEAR
$ANALYZE BL R C F COMP.00%%COMP
$IF %NROWS > %%MINROW THEN GOTO LOOP
$ANALYZE BL C C F COMP.00%%COMP
$IF %NCOLS > %%MINCOL THEN GOTO LOOP
$SET MINBLK = COMP.00%%COMP
:LOOP
$CALC COMP = COMP - 1
$IF %%COMP > 0 THEN GOTO NEXTBLK
$ENDLOOP

$IF %%MINBLK = 0 THEN GOTO RETURN
* WE HAVE BLOCK WITH FEWEST NUMBER OF INFEASIBLE ROW(S)
$ANALYZE SUB CLEAR
$ANALYZE BL R C F %%MINBLK
$ANALYZE BL C C F %%MINBLK
$ANALYZE SUB SET
$IF %NROWS > 1 THEN SKIP 2
 The following equation is infeasible.
$SKIP 1
 The following equations are infeasible.
$ANALYZE LIST //E
$TEXT
  Its column bounds are:
$SET N = %NROWS
$CALC N = N + %NCOLS
$IF %%N > 15 THEN ANALYZE _PAUS
$ANALYZE DISP COL //LO,UP
$SET NR = %NROWS
$ANALYZE SUB A R *
$ANALYZE SUB D R * P=0
$ANALYZE BL R C TO TEMP
$ANALYZE SUB D R *
$ANALYZE BL R C F TEMP
$ANALYZE SUB D R %OBJ
$ANALYZE BL R E TEMP
$ANALYZE SUB SET

* SUBMATRIX NOW HAS ALL ROWS WITH NONZERO PRICE THAT INTERSECT
* THE COMPONENT'S COLUMNS, EXCEPT THOSE THAT WERE IN THE BLOCK
* (AND ALREADY LISTED)

$IF %NROWS = 0 THEN GOTO EXIT
$IF %NROWS > 2 THEN ANALYZE _PAUSE
$TEXT
$ANALYZE SUB COL *
 The activity levels, however, are also constrained by:
$ANALYZE L  //E
$STACK

$LOOP
$IF %VCOLLO <> 0 THEN SKIP LOOP
$IF %VCOLUP =  * THEN PUSH 1 %COLUMN
$NEXT COL

$LOOP
:NEXTCOL
$IF %NSTACK1 = 0 THEN ENDLOOP
$POP 1 COL
$ANALYZE SUB D C %%COL
$GOTO NEXTCOL
$ENDLOOP

$ANALYZE SUB SET
$IF %NCOLS = 0 THEN GOTO EXIT
$IF %NCOLS > 2 THEN ANALYZE _PAUS
 Note the bounds:
$ANALYZE DISP //LO,UP
$GOTO EXIT

:RETURN
