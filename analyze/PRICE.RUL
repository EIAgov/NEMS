*                ::: PRICE.RUL :::
*
* CHANGED 9-20-90, 7-26-90, 10-24-91, 7-30-93

* THIS ATTEMPTS TO INTERPRET PRICE OF A ROW, SET, SAY, BY SUBMAT R name

* SEE IF ROW IS PASSED AS PARAM (%%1)
$IF @%%1 <> @ THEN ANALYZE SUBMAT ROW %%1
* CHECK THAT A ROW WAS SET
$IF %NROWS > 0 THEN GOTO CHECK1
$IF @%%1 <> @ THEN SKIP 2
 No row specified.
$EXIT
 %%1 is not a recognized row name.
$EXIT

:CHECK1
* CHECK THAT ONLY 1 ROW WAS SET
$IF %NROWS = 1 THEN SKIP 2
 More than 1 row was specified...Please INTERPRT 1 at a time
$EXIT

$IF %ROW = %OBJ THEN INTERPRT OBJECTIV
* REMEMBER ROW AND ITS PRICE
$SET ROW = %ROW
$SET PRICE = %VROWP
$IF %SWSYN = F THEN ANALYZE READ SYNTAX

* ROW IS NOT OBJ...
$IF %STATUS <> OPTIMAL THEN GOTO RATE

* SOLUTION IS OPTIMAL
* GIVE MEANING OF THE ROW TO START WITH (IF WE HAVE SYNTAX)
$IF %SWSYN = T THEN ANALYZE EXPLAIN ROW %%ROW
* SEE IF ROW IS SLACK
$IF %%PRICE <> 0 THEN SKIP 2
 This row is not binding, so its marginal price = 0.
$EXIT

* PUT MARGIN-SETTING COLUMNS INTO SUBMATRIX
$ANALYZE SUBMAT  CO * D=0
$ANALYZE TRACE
* SAVE SUBMATRIX
$ANALYZE BLOCK ROW ERASE *
$ANALYZE BLOCK COL ERASE *
$ANALYZE BLOCK ROW COPY TO ROWS
$ANALYZE BLOCK COL COPY TO COLS
$ANALYZE BLOCK SAVE
* SEE IF THERE IS A SINGLETON BASIC COLUMN IN THIS ROW
$ANALYZE SUBMAT D R %OBJ
$ANALYZE SUBMAT SET

$LOOP
$IF %NZCSUB > 1 THEN SKIP LOOP
$IF %VCOLD <> 0 THEN SKIP LOOP
$LOOKUP NZ %%ROW, %COLUMN
$IF %VLOOK = 0 THEN SKIP LOOP
* WE HAVE SINGLETON BASIC COLUMN IN THIS ROW...SIMPLE EXPLANATION
$SET COL = %COLUMN
$IF %VLOOK <> 1 THEN GOTO SCALE
$IF %SWSYN = T THEN SKIP 2
The price of this row (%%PRICE) is the %OBJ of column %%COL .
$GOTO EXIT
The price of this row (%%PRICE) is the %OBJ of column %%COL ...
$ANALYZE EXPLAIN COL %%COL
$GOTO EXIT

:SCALE
The price of this row (%%PRICE) is the %OBJ of column %%COL (%VCOLD)
 divided by the %VLOOK units it has in row %%ROW .
$ANALYZE SHO //ARRAY,SYN
$GOTO EXIT
$NEXT C

* IF WE GET HERE, NO SINGLETON BASIC COLUMN EXPLAINS THE PRICE
* RESTORE SUBMATRIX
$ANALYZE BLOCK LOAD
$ANALYZE BLOCK ROW COPY FROM ROWS
$ANALYZE BLOCK COL COPY FROM COLS
$ANALYZE SUBMAT SET
$ANALYZE BLOCK ROW ERASE *
$ANALYZE BLOCK COL ERASE *

* NOW THE INTERPRETATION IS DEPENDENT ON THE ROW CLASS
* (AND HENCE THE MODEL)

$IF %PROBLEM = WOODNET THEN GOTO OK
$IF %PROBLEM = BLENDRT THEN GOTO OK
$IF %PROBLEM <> REFINERY THEN GOTO ENDREF
INCLUDE PRICER.RUL
:ENDREF
$GOTO RATE

:OK
 I shall try to interpret its price of $%%PRICE.
$INTERPRT PRICE%%ROW(1:1)

:EXIT
$ANALYZE BLOCK ROW ERASE *
$ANALYZE BLOCK COL ERASE *
$EXIT

:RATE
* THIS IS GENERIC INTERPRETATION...USING RATEOF
$LOOKUP Y %%ROW
$SET V = %VLOOK
Note the following effects (ie, rates of substitution) a change in the
level of row %%ROW has on the basic activity levels.  The "Net Rate" is
the amount each basic level must change times the %OBJ of the basic activity.
The sum of these effects is the price of %%ROW, which is the net rate at
which %OBJ changes with respect to a slight change in the level of %%ROW,
which is currently = %%V .
$IF %SWOTFIL = F THEN ANALYZE _PAUSE
$ANALYZE RATEOF ROW %%ROW //P
$EXIT
                  ::: END PRICE.RUL :::
