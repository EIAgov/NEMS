* PRICEB.RUL
* THIS IS PRICE INTERPRETATION FOR ROW WHOSE NAME BEGINS
* WITH B.  IN MANY MODELS, THIS IS A BALANCE ROW, BUT HERE
* WE SPECIFICALLY ASSUME THE LP MODEL, BLENDRT.

* THIS IS INSTANTIATED FROM PRICE.RUL WITH SUBMATRIX AFTER TRACE

$IF %PROBLEM <> BLENDRT THEN GOTO SORRY
*$TEXT
* =================== NOTE ===================
*$TEXT CENTER
*  This uses both the GRAPH and the RATEOF commands, so you must be
*  using the Graph version (Standard and Views versions will not
*  function properly).
*$TEXT
*$ANALYZE _PAUSE

* SET DOMAIN SETS
$SET MT = %%ROW(2:3)
$SET R  = %%ROW(4:4)
$SET T =  %%ROW(5:5)

$SET LINE = 6
$SET NUMBER = 0
$SET COST = 0
$SET NC = 0
$SET NO = 0
$SET NT = 0
$SET NI = 0
$SET NP = 0

* CLEAR STACKS
$STACK

* RE-DO TRACE TO KEEP ONLY 1 ACTIVITY WITH POS COEF IN ROW
$ANALYZE SUBMAT ROW %%ROW
$ANALYZE SUBMAT COL * S=B
$ANALYZE SUBMAT SET
$IF %NCOLS = 0 THEN GOTO UP
$LOOP
$LOOKUP NZ %%ROW %COLUMN
$IF %VLOOK < 0 THEN SKIP LOOP
$IF %VLOOK <> 1 THEN SKIP 2
$SET COL = %COLUMN
$GOTO GOTCHA
$PUSH 1 %COLUMN
$NEXT COL
$IF %NSTACK1 > 0 THEN GOTO POP

* MUST HAVE POS ENTRY AT UPPER BOUND
:UP
$ANALYZE SUBMAT COL * S=U
$ANALYZE SUBMAT ROW %%ROW
$ANALYZE SUBMAT SET
$LOOP
$LOOKUP NZ %%ROW %COLUMN
$IF %VLOOK < 0 THEN SKIP LOOP
$SET COL = %COLUMN
$GOTO GOTCHA
$NEXT COL

:POP
$POP 1 COL

:GOTCHA
$ANALYZE SUBMAT COL %%COL
$ANALYZE SUBMAT SET
$ANALYZE TRACE
$ANALYZE GRAPH SET *
$ANALYZE GRAPH SAVE

* NOW SUBMATRIX HAS MARGIN-SETTING PATH, SAVED AS GRAPH

*     LOOP TO GET COST AND RENT
* =========================================================
$LOOP
$CALC N%COLUMN(1:1) = N%COLUMN(1:1) + 1
$CALC COST = COST + %VCOLC
$PUSH 1  %COLUMN
$CALC NC = NC + 1
$NEXT COL
* =========================================================
$SET RENT = 0
$CALC RENT = %%COST - %%PRICE
* RENT IS NOT ACTUALLY RENT...WE COULD HAVE COEF <> 1, WHICH
* WAS NOT ACCOUNTED FOR IN COMPUTING %%COST
$IF %%RENT <> 0 THEN GOTO GRAPH
 The following flow graph gives the relevant flow of
$ENTITY %%MT MT
  into
$ENTITY %%R R
  at
$ENTITY %%T T
  .
$ANALYZE GRAPH DISP ROW //SYN
$ANALYZE _PAUSE
$SET LINE = 3
$TEXT
  The arcs are from the following %%NC activities with nonzero costs.
$ANALYZE L COST
$CALC LINE = LINE + %%NC
$IF %%LINE > 22 THEN ANALYZE _PAUSE
$ANALYZE EXPLAIN COL
$ANALYZE _PAUSE

$TEXT
  Delivered cost = $%%COST = marginal price of %%ROW .
$TEXT CENTER
 You can also see the activity sequence in the following graph.
$ANALYZE GRAPH DISP COL
$EXIT

:GRAPH
$CALC LINE = LINE + 2
$CALC NUMBER = 3 * %NROWS
$CALC NUMBER = NUMBER + %%LINE
$IF %%NUMBER < 23 THEN GOTO GRAPHROW
$ANALYZE _PAUSE
$SET LINE = 0

:GRAPHROW
*$ANALYZE GRAPH DISP R
*$CALC NUMBER = 3 * %NROWS
*$CALC LINE = LINE + %%NUMBER
*$CALC NUMBER = 3 * %%NO
*$CALC LINE = LINE + %%NUMBER
*$IF %%LINE < 23 THEN SKIP 2
*$ANALYZE _PAUSE
*$SET LINE = 0
*................Activate the above lines to display row digraph
$TEXT
  The relevant flow of materials is given in the following graph.
$TEXT

* FORM (FROM OPERATING PROCESSING UNITS)
$IF %%NO = 0 THEN GOTO PLACE
$TEXT
 Those from operation of processing units:
$CALC LINE = LINE + 2
$ANALYZE GRAPH LOAD
$ANALYZE GRAPH COPY
$ANALYZE SUBMAT SET
$ANALYZE GRAPH SET  * //FORM
$ANALYZE GRAPH DISP R //SYN

:PLACE
$IF %%NT = 0 THEN GOTO TIME
$CALC NUMBER = 3 * %%NT
$CALC LINE = LINE + %%NUMBER
$IF %%LINE < 23 THEN SKIP 2
$ANALYZE _PAUSE
$SET LINE = 0

$TEXT
 Those from transportation:
$CALC LINE = LINE + 2
$ANALYZE GRAPH LOAD
$ANALYZE GRAPH COPY
$ANALYZE SUBMAT SET
$ANALYZE GRAPH SET * //PLACE
$ANALYZE GRAPH DISP R //SYN

:TIME
$IF %%NI = 0 THEN GOTO ENDGRPH
$CALC NUMBER = 3 * %%NI
$CALC LINE = LINE + %%NUMBER
$IF %%LINE < 23 THEN SKIP 2
$ANALYZE _PAUSE
$SET LINE = 0

$TEXT
 Those from carrying inventory:
$CALC LINE = LINE + 2
$ANALYZE GRAPH LOAD
$ANALYZE GRAPH COPY
$ANALYZE SUBMAT SET
$ANALYZE GRAPH SET * //TIME
$ANALYZE GRAPH DISP R //SYN
:ENDGRPH

:RENT
$ANALYZE _PAUSE
$TEXT
  The full impact of slightly changing the level of %%ROW can be seen
  from the following rates of substitution, where
  the marginal price (%%PRICE ) is the net rate at which %OBJ
  (objective row) changes with respect to the level of %%ROW .  The
  sum of the Net Rates = Net effect of
$LOOKUP YL %%ROW
$IF %VLOOK = 0 THEN SKIP 2
  changing the demand for
$SKIP 2
  having a stockpile (ie, making the RHS negative) or a local
  demand (ie, making the RHS positive) of
$ENTITY %%MT MT
  in
$ENTITY %%R R
  at
$ENTITY %%T T
  .
$TEXT 2
$ANALYZE _PAUSE
$ANALYZE RATEOF ROW %%ROW //EQ OBJ COL
 To see these rates in syntax form, enter RATEOF //SYN COL
$EXIT

:SORRY
 Sorry, no interpretation is available for %%ROW in %PROBLEM .
$EXIT
