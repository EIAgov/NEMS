* DRATE.RUL - INTERPRT D USING RATES
INCLUDE SHIFT.RUL

* SEE IF COL IS PASSED AS PARAM (%%1)
$IF @%%1 = @ THEN GOTO BEGIN
$ANALYZE SUBMAT COL %%1
$ANALYZE STRING K=2
$LOOP
:LOOP
$IF @%%''K = @ THEN ENDLOOP
$ANALYZE SUB AD COL %%''K
$ANALYZE STRING INCREMENT K
$GOTO LOOP
$ENDLOOP

:BEGIN
* CHECK THAT A COL WAS SET
$IF %NCOLS > 0 THEN SKIP 2
 No activity was specified (set submatrix, or pass name directly).
$EXIT

$IF %STATUS(1:6) <> INFEAS THEN GOTO SET
$ANALYZE STRING OBJ = the sum of infeasibilites
$ANALYZE STRING OBCOEF = infeasibility coefficient (0, 1 or -1)
$GOTO INTRO

:SET
$ANALYZE STRING OBJ = %OBJ
$ANALYZE STRING OBCOEF = %OBJ coefficient
$IF %STATUS = OPTIMAL THEN ANALYZE STRING OBJ = %OPT(1:3) %OBJ
:INTRO
  Each activity has a "reduced cost", denoted by D, which is the
  rate at which 'OBJ changes with respect to a change in its bound
  value.  The change in a non-basic variable (at the bound)
  causes some of the basic variables to change.  The rate at which a
  basic variable changes is called the "rate of substitution".
  Multiplying each rate of substitution times the 'OBCOEF is a factor
  of the reduced cost.  (D = 'OBCOEF + sum of factors = direct effect
  + indirect effect.)
$TEXT 2
$ANALYZE STRING OBJ
$ANALYZE STRING OBCOEF

$ANALYZE SUBMAT DEL COL * S=BI
$ANALYZE SUBMAT ROW *
$ANALYZE SUBMAT SET
$IF %NCOLS > 0 THEN GOTO DOIT
 The submatrix has no nonbasic variable, so there is no reduced cost
 to interpret.  Either set the submatrix to the nonbasics whose reduced
 cost you want to interpret, or pass a name mask, as INTERP D *.
$EXIT

:DOIT
$STACK
$LOOP
$QUEUE 1 %COLUMN
$NEXT COL

$IF %SWSYN = F THEN ANALYZE READ SYN
$LOOP
:LOOP
$POP 1 COL
$IF @%%COL = @ THEN GOTO DONE
$ANALYZE _PAUSE
$IF %SWSYN = T THEN ANALYZE EXPLAIN COL %%COL
$ANALYZE RATE COL %%COL //D
$GOTO LOOP
$ENDLOOP

:DONE
$IF %SWSYN = F THEN EXIT
 Specify  RATEOF //SYN to see these rates in English sentences
 (you can also specify RATE //SYN,COL to see only basic columns affected).
$EXIT
