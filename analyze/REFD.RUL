* RULE FILE FOR DJ OF REFINERY LP
$VECTOR CLEAR
$IF %NCOLS <> 1 THEN ANALYZE SUB COL A S=L
$SET FUEL = %COLUMN(4:4)
$SET STOCK = %COLUMN(2:3)
An increase in the allocation of
$ENTITY %%STOCK BS
to make fuel %%FUEL
(current level = %VCOLX) has a direct cost of $%VCOLC .  The indirect
effects for one unit of increase are as follows.

$TEXT 2
$TEXT TAB 3,13,20,35,37,52,54
*         1 2  3  4  5  6  7
$SET UNDERL = ================
%NTAB(1)Allocation %NTAB(3)Change %NTAB(4)@ %NTAB(6)Giving net
$TEXT
stock --->%NTAB(2)fuel %NTAB(3)in Level %NTAB(4)%OBJ %NTAB(6)%OBJ Change
$TEXT
%%UNDERL %NTAB(3)%%UNDERL(1:12) %NTAB(4)%%UNDERL(1:12) %NTAB(6)%%UNDERL(1:12)
$TEXT

$VECTOR RATE COL %COLUMN

$SET DOLLAR = $
$SET I = 1
$SET IC = 0
$LOOP
:LOOP
$SET A = %_NAME(%%I)
$IF %%A(1:1) <> A THEN GOTO NEXT
$SET STOCK = %%A(2:3)
$SET FUEL  = %%A(4:4)
$LOOKUP NZ, %OBJ,%%A
$SET V = %VLOOK
$CALC V = %%V * %VECTOR(%%I)
$IF %%STOCK = AL THEN SET STOCK = alkylate
$IF %%STOCK = CC THEN SET STOCK = cat. cracked
$IF %%STOCK = IS THEN SET STOCK = ispentane
$IF %%STOCK = SR THEN SET STOCK = straight run
%%STOCK %NTAB(2) %%FUEL %NTAB(3) %VECTOR(%%I)
%NTAB(4) $ %NTAB(5)%VLOOK %NTAB(6)%%DOLLAR %NTAB(7)%%V
$CALC IC = IC + %%V
$TEXT
$SET DOLLAR =
:NEXT
$IF %%I >= %NVECTOR THEN ENDLOOP
$CALC I = I + 1
$GOTO LOOP
$NEXT COL

%NTAB(6) ============
$TEXT
%NTAB(3)  Total Indirect COST: %NTAB(6)$%NTAB(7)%%IC
$TEXT 2
The total effect is the sum of direct ($%VCOLC) + indirect
$IF %%IC >= 0 THEN SKIP 2
$SET DOLLAR = -
$CALC IC = 0 - %%IC
(%%DOLLAR $%%IC), which is $%VCOLD (approximately).
$EXIT
